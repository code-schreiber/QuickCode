// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'org.ajoberstar:grgit:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

plugins {
    id "org.sonarqube" version "2.2"
}

allprojects {
    repositories {
        jcenter()
    }
}

import org.ajoberstar.grgit.Grgit


ext {
    def version = "0.0.1"
    def git = Grgit.open(currentDir: projectDir)
    versionCode = getVersionCode(version, git)
    versionName = getVersionName(git, version)

    compileSdkVersion = 25
    buildToolsVersion = "25.0.2"
    minSdkVersion = 23
    targetSdkVersion = 25

    googleSupportLibrary="25.2.0"
}

static def getVersionCode(version, git) {
    def versionCode = version.replaceAll("\\.", "")
    def postfix = git.head().time// TODO use git.tag.list().size() as soon as we start tagging
    def substring = (versionCode + postfix).substring(0, 10)
    return Integer.parseInt(substring)
}

static def getVersionName(git, version) {
    return version + "-" + git.describe() + "-" + git.branch.getCurrent().name
}

task printVersion() {
    println("Version Code: $versionCode")
    println("Version Name: $versionName")
}

sonarqube {
    properties {
        property "sonar.projectName", project.name
        property "sonar.projectKey", project.name
        property "sonar.projectVersion", rootProject.versionName

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarqube.com"

        property "sonar.android.lint.report", "mobile/build/outputs/lint-results-debug.xml"
        property "sonar.import_unknown_files", true
        property "sonar.exclusions","**/*.png,**/*.jpg,**/*.pdf,**/*.ttf,**/*.otf,"
        
        //    # Comma-separated paths to directories with sources
        //    # sonar.sources = "."
        //    # path to test source directories
        //    # sonar.tests=libraryTest/src
        //    # Tell SonarQube where to get the code coverage reports
        //    # sonar.emma.reportPath=target/emma
    }
}
