// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath 'com.google.firebase:firebase-plugins:1.1.4'
    }
}

plugins {
    id "org.sonarqube" version "2.6"
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}

import org.ajoberstar.grgit.Grgit


ext {
    def git = Grgit.open(currentDir: projectDir)
    versionCode = git.tag.list().size()
    versionName = git.describe()

    compileSdkVersion = 26
    buildToolsVersion = "26.0.2"
    minSdkVersion = 23
    targetSdkVersion = 26

    googleSupportLibrary = "27.0.0"
    googleServices = "11.4.2"
}

task printVersion() {
    println("Version Code: $versionCode")
    println("Version Name: $versionName")
}

sonarqube {
    properties {
        property "sonar.projectName", project.name
        property "sonar.projectKey", project.name
        property "sonar.projectVersion", rootProject.versionName

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.android.lint.report", "mobile/build/outputs/lint-results-debug.xml"
        property "sonar.import_unknown_files", true
        property "sonar.exclusions", "**/*.png,**/*.jpg,**/*.pdf,**/*.ttf,**/*.otf,"
    }
}
